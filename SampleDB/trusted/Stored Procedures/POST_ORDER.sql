CREATE PROCEDURE [trusted].[POST_ORDER]
(
	/*Returns Json*/
	/*Single Result*/

	@PERSON_ID INT,
	@ORDER_NAME VARCHAR(16),
	@ORDER_ITEMS trusted.POST_ORDER_ITEM_UDTT READONLY,

	@SQL_ERROR_ID INT OUTPUT,
	@MESSAGE_RESULT VARCHAR(256) OUTPUT
)
AS
BEGIN
	
	IF @PERSON_ID IS NULL THROW 50001, '@PERSON_ID', 1;
	IF @ORDER_NAME IS NULL THROW 50001, '@ORDER_NAME', 1;
	IF NOT EXISTS (SELECT 1 FROM @ORDER_ITEMS) THROW 50001, '@ORDER_ITEMS',1;

	DECLARE
		@ORDER_ID INT,
		@CURRENT_DATETIME DATETIME2(0) = SYSDATETIME()

	BEGIN TRY
		
		DECLARE @ORDER_RESULT TABLE
		(
			PERSON_ORDER_ID INT
		)

		INSERT INTO dbo.[ORDER]
		(
			PERSON_ID,
			ORDER_NAME,
			ACTIVE_START
		)
		OUTPUT inserted.ORDER_ID INTO @ORDER_RESULT(PERSON_ORDER_ID)
		VALUES
		(
			@PERSON_ID,
			@ORDER_NAME,
			@CURRENT_DATETIME
		)

		SELECT TOP 1 @ORDER_ID = PERSON_ORDER_ID FROM @ORDER_RESULT;

		INSERT INTO dbo.ORDER_ITEM
		(
			ORDER_ID,
			NAME,
			COMMENTS
		)
		SELECT
			@ORDER_ID,
			NAME,
			COMMENTS
		FROM
			@ORDER_ITEMS;

		SELECT
			O.ORDER_ID orderId,
			O.ORDER_NAME orderName,
			O.ACTIVE_START orderDate,
			(SELECT
				OI.ORDER_ITEM_ID itemId,
				OI.NAME name,
				OI.COMMENTS comments
			FROM
				dbo.ORDER_ITEM OI
			WHERE
				O.ORDER_ID = OI.ORDER_ID
			FOR JSON PATH) items
		FROM
			dbo.[ORDER] O
		WHERE
			O.ORDER_ID = @ORDER_ID
		FOR JSON PATH, WITHOUT_ARRAY_WRAPPER;

		RETURN 200;
			
	END TRY
	BEGIN CATCH

		SET @SQL_ERROR_ID = 1001;
		SET @MESSAGE_RESULT = (SELECT 500 AS MESSAGE_ID FOR JSON PATH, WITHOUT_ARRAY_WRAPPER);
		
		RETURN 500;

	END CATCH
END;