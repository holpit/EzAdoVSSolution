CREATE PROCEDURE [trusted].[POST_ORDER]
(
	/*Returns Json*/
	/*Single Result*/

	@PERSON_ID INT,
	@ORDER_NAME VARCHAR(16),
	@ORDER_ITEMS trusted.POST_ORDER_ITEM_UDTT READONLY,

	@SQL_ERROR_ID INT OUTPUT,
	@MESSAGE_RESULT VARCHAR(256) OUTPUT
)
AS
BEGIN
	
	IF @PERSON_ID IS NULL THROW 50001, '@PERSON_ID', 1;
	IF @ORDER_NAME IS NULL THROW 50001, '@ORDER_NAME', 1;
	IF NOT EXISTS (SELECT 1 FROM @ORDER_ITEMS) THROW 50001, '@ORDER_ITEMS',1;

	DECLARE
		@PERSON_ORDER_ID INT,
		@CURRENT_DATETIME DATETIME2(0) = SYSDATETIME()

	BEGIN TRY
		
		DECLARE @PERSON_ORDER_RESULT TABLE
		(
			PERSON_ORDER_ID INT
		)

		INSERT INTO PERSON_ORDER
		(
			PERSON_ID,
			ORDER_NAME,
			ACTIVE_START
		)
		OUTPUT inserted.PERSON_ORDER_ID INTO @PERSON_ORDER_RESULT(PERSON_ORDER_ID)
		VALUES
		(
			@PERSON_ID,
			@ORDER_NAME,
			@CURRENT_DATETIME
		)

		SELECT TOP 1 @PERSON_ORDER_ID = PERSON_ORDER_ID FROM @PERSON_ORDER_RESULT;

		INSERT INTO dbo.ORDER_ITEM
		(
			ORDER_ID,
			NAME,
			COMMENTS
		)
		SELECT
			@PERSON_ORDER_ID,
			NAME,
			COMMENTS
		FROM
			@ORDER_ITEMS;

		SELECT
			PO.PERSON_ORDER_ID orderId,
			PO.ORDER_NAME orderName,
			PO.ACTIVE_START orderDate,
			(SELECT
				OI.ORDER_ITEM_ID itemId,
				OI.NAME name,
				OI.COMMENTS comments
			FROM
				dbo.ORDER_ITEM OI
			WHERE
				PO.PERSON_ORDER_ID = OI.ORDER_ID
			FOR JSON PATH) items
		FROM
			dbo.PERSON_ORDER PO
		WHERE
			PO.PERSON_ORDER_ID = @PERSON_ORDER_ID
		FOR JSON PATH, WITHOUT_ARRAY_WRAPPER;

		RETURN 200;
			
	END TRY
	BEGIN CATCH

		SET @SQL_ERROR_ID = 1001;
		SET @MESSAGE_RESULT = (SELECT 500 AS MESSAGE_ID FOR JSON PATH, WITHOUT_ARRAY_WRAPPER);
		
		RETURN 500;

	END CATCH
END;